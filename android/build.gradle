apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

def safeExtGet(prop, fallback) {
    return rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    namespace "com.faceliveness"
    compileSdkVersion safeExtGet('compileSdkVersion', 35)
    ndkVersion safeExtGet('ndkVersion', '26.1.10909125')

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 21)
        targetSdkVersion safeExtGet('targetSdkVersion', 35)

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -frtti -fexceptions"
                abiFilters 'armeabi-v7a', 'arm64-v8a'
                arguments "-DANDROID_ARM_NEON=TRUE"
                arguments "-DANDROID_TOOLCHAIN=clang"
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    sourceSets {
        main {
            assets.srcDirs = ["src/main/assets"]
            jniLibs.srcDirs = ["src/main/jniLibs"]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    def cameraXVersion = safeExtGet('cameraXVersion', '1.3.3')
    def cameraViewVersion = safeExtGet('cameraViewVersion', cameraXVersion)

    implementation "org.jetbrains.kotlin:kotlin-stdlib:${safeExtGet('kotlinVersion', '1.9.22')}"
    implementation "androidx.core:core-ktx:${safeExtGet('androidxCoreVersion', '1.12.0')}"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:${safeExtGet('androidxLifecycleVersion', '2.7.0')}"
    implementation "androidx.camera:camera-core:${cameraXVersion}"
    implementation "androidx.camera:camera-camera2:${cameraXVersion}"
    implementation "androidx.camera:camera-lifecycle:${cameraXVersion}"
    implementation "androidx.camera:camera-view:${cameraViewVersion}"
    implementation 'com.facebook.react:react-native:+'
}
